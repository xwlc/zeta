# SPDX-License-Identifier: GPL-3.0-only OR Apache-2.0 OR MIT
# SPDX-FileCopyrightText: 2023 Charles Wong <charlie-wong@outlook.com>
# Created By: Charles Wong 2023-10-19T20:13:29+08:00 Asia/Shanghai
# Repository: https://github.com/xwlc/zeta

# 调用 etc/shell/bin 目录下脚本, 如 `xcmd date-time`
function xcmd() {
  [[ $# -eq 0 ]] && return 255

  local sf="${ZETA_DIR}/xsh/bin/$1"; shift
  local xcolors="${ZETA_DIR}/xsh/colors.xsh"

  if [[ ! -f "${sf}" ]]; then
    @zeta:xsh:emsg "do not exist $(@D9 ${sf}) script file."; return 255
  fi

  env XCOLORS="${xcolors}"  "${sf}"  "$@"
}

if [[ -n "${ZSH_VERSION:-}" ]]; then
  function @zeta:comp:xcmd() {
    local context state state_descr line opt_args zargs
    function once±zcomp1() {
      local zcmds=( $(command ls --hide='*.log' "${ZETA_DIR}/xsh/bin" | sort) )
      _describe 'command' zcmds; unset -f once±zcomp1
    }
    # 命令名 ${line[1]}  命令参数 ${line[2]} ...
    _arguments '1:脚本名称:once±zcomp1' ':后续参数:->todo'
    if [[ "todo" == "${state[1]}" ]]; then
      case "${line[1]}" in
        touch) # 补全 xcmd touch 参数的子目录部分
          # _path_files 内置函数, -/ 仅补全目录名
          _arguments '2:补全子目录:_path_files -/' ;;
        *) return ;;
      esac
    fi
  }
  compdef @zeta:comp:xcmd xcmd
elif [[ -n "${BASH_VERSION:-}" ]]; then
  function @zeta:comp:xcmd() {
    COMPREPLY=( $(compgen -W "$(command ls --hide='*.log' "${ZETA_DIR}/xsh/bin" | sort)" -- "$2") )
  }
  complete -F @zeta:comp:xcmd xcmd
fi
